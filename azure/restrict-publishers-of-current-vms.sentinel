# This policy uses the Sentinel tfstate/v2 import to require that
# all existing Azure VMs have publishers from a specified list

# Import common-functions/tfstate-functions/tfstate-functions.sentinel
# with alias "state"
import "tfstate-functions" as state

# List of allowed publishers
# Include "null" to allow missing or computed values
allowed_publishers = ["RedHat", "Canonical", "MicrosoftWindowsServer"]

# Get all Azure VMs using azurerm_virtual_machine
allAzureVMs = state.resources("azurerm_virtual_machine")

# Filter to Azure VMs with violations that use azurerm_virtual_machine
# Warnings will be printed for all violations since the last parameter is true
violatingAzureVMs = state.attribute_not_in_list(allAzureVMs,
  "storage_image_reference.0.publisher", allowed_publishers, true)

# Get all Azure Windows VMs using azurerm_windows_virtual_machine
allAzureWindowsVMs = state.resources("azurerm_windows_virtual_machine")

# Filter to Azure Windows VMs with violations that use azurerm_windows_virtual_machine
# Warnings will be printed for all violations since the last parameter is true
violatingAzureWindowsVMs = state.attribute_not_in_list(allAzureWindowsVMs,
  "source_image_reference.0.publisher", allowed_publishers, true)

# Get all Azure Linux VMs using azurerm_linux_virtual_machine
allAzureLinuxVMs = state.resources("azurerm_linux_virtual_machine")

# Filter to Azure Linux VMs with violations that use azurerm_linux_virtual_machine
# Warnings will be printed for all violations since the last parameter is true
violatingAzureLinuxVMs = state.attribute_not_in_list(allAzureLinuxVMs,
  "source_image_reference.0.publisher", allowed_publishers, true)

# Main rule
violations = length(violatingAzureVMs["messages"]) +
             length(violatingAzureWindowsVMs["messages"]) +
             length(violatingAzureLinuxVMs["messages"])

main = rule {
  violations is 0
}
