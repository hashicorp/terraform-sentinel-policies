# This policy uses the Sentinel tfplan/v2 import to require that
# all VMware VMs obey a disk limit

# Import common-functions/tfplan-functions/tfplan-functions.sentinel
# with alias "plan"
import "tfplan-functions" as plan
import "general-functions" as general

# Disk Size Limit (in GB)
maxDiskSize = 100

# Get all VMs
allVMs = plan.resources("vsphere_virtual_machine")

# Validate VM Disks
disksValidated = true
for allVMs as address, vm {

  # Find the disks of the current VM
  disks = plan.blocks(vm, "disk")

  # Filter to violating disks of the current VM
  # Warnings will not be printed for violations since the last parameter is false
  violatingDisks = plan.attribute_greater_than_value(disks,
                   "size", maxDiskSize, false)

  # Print warnings if any violating disks
  if length(violatingDisks["messages"]) > 0 {
    disksValidated = false
    print(address, "has at least one disk with size greater than", maxDiskSize)
    general.print_violations(violatingDisks["messages"], "Disk")
  } // end if

} // end for VMs

# Main rule
main = rule {
  disksValidated
}
